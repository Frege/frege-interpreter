
buildscript {
    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url = "https://oss.sonatype.org/content/groups/public"
        }
        mavenCentral()
    }
    dependencies {
        classpath "gradle.plugin.org.frege-lang:frege-gradle-plugin:0.5"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

apply plugin: 'io.codearte.nexus-staging'

allprojects {
    defaultTasks "build"

    tasks.withType(JavaCompile) {
       options.encoding = 'UTF-8'
    }
}

subprojects {

    def urlFile = { url, name ->
        File file = new File("$buildDir/download/${name}.jar")
        file.parentFile.mkdirs()
        if (!file.exists()) {
            new URL(url).withInputStream { downloadStream ->
                file.withOutputStream { fileOut ->
                    fileOut << downloadStream
                }
            }
        }
        files(file.absolutePath)
    }
    
    apply plugin: "java"
    apply plugin: "groovy"
    apply plugin: "maven"
    apply plugin: "signing"

    ext {
        baseVersion = "1.3"
        isSnapshot = true
        snapshotAppendix = "-SNAPSHOT"
        projectVersion = baseVersion + (isSnapshot ? snapshotAppendix : "")
        fregeReleaseName = "3.24public"
        fregeVersion = "3.24.405"
    }

    apply from: "$rootDir/gradle/sonatype.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    version = projectVersion
    group = groupName
    archivesBaseName = project.name

    repositories {
        mavenLocal()
        maven {
            url = sonatypeRepositoryUrl
        }
        mavenCentral()
    }

    dependencies {
        compile urlFile("https://github.com/Frege/frege/releases/download/$fregeReleaseName/frege${fregeVersion}.jar", "frege${fregeVersion}")
    }

    nexusStaging {
        packageGroup = groupName
        stagingProfileId = "a622b6773bea07"
    }

}

